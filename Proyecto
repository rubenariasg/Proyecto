Funcion opcion=presentarMenu(titulo,menu,lim)
	Definir opcion Como Caracter
	Definir pos Como Entero
	Borrar Pantalla
	Escribir titulo
	Para pos=1 Hasta lim Con Paso 1 Hacer
		Escribir menu[pos]
	Fin Para
	Escribir "       Elija opción[1..",lim,"]" Sin Saltar
	leer opcion
FinFuncion



// =======================================================Estructuras Selectivas=======================================================         

//Ejercicio 1.
//¿Siguiendo la prioridad de operadores, convierta a expresión matemática,
//resuelva e indique en cuál tipo de variable almacenará el resultado de las
//siguientes expresiones:
//(5 + 3 * 2) + 9 > 3 * 5 * 14 % 3
//2 *(4 ? 10 + 8)/2* 36 *(1/2)
//260 / 12 + 54 % 3 ? 85 % 7
//(48 < 2 * 3) | | (2 * 7 < 12)
//((8 > 2) | | (932 < 23) ) & 4 == 2
Funcion Ejercicio_N1
	ESCRIBIR "¿Siguiendo la prioridad de operadores, convierta a expresión matemática, resuelva e indique en cuál tipo de variable almacenará el resultado de las siguientes expresiones:"
	DEFINIR  resultado2, resultado3 COMO real
	DEFINIR resultado4, resultado5,resultado1 COMO Logico
	resultado1 <- (5 + 3 * 2) + 9 > 3 * 5 * 14 % 3
	resultado2 <- 2 * (4 - 10 + 8) / 2 * 36
	resultado3 <- 260 / 12 + 54 % 3 - 85 % 7
	resultado4 <- (48 < 2 * 3) | (2 * 7 < 12)
	resultado5 <- ((8 > 2) | (932 < 23)) & 4 == 2
	Escribir "El resultado de la primera función es: ", resultado1
	Escribir "El resultado de la segunda función es: ", resultado2
	Escribir "El resultado de la tercera función es: ", resultado3
	Escribir "El resultado de la cuarta función es: ", resultado4
	Escribir "El resultado de la quinta función es: ", resultado5
	
FinFuncion


//Ejercicio 2.
//Dados dos (2) números calcule la suma, resta, multiplicación, división y módulo.

Funcion  Ejercicio_N2
	Definir num1, num2, suma, resta, multiplicación, división, módulo Como Real
	
	Escribir "Ingrese el primer número:"
	Leer num1
	Escribir "Ingrese el segundo número:"
	Leer num2
	
	suma = num1 + num2
	resta = num1 - num2
	multiplicación = num1 * num2
	división = num1 / num2
	módulo = num1 % num2
	
	Escribir "La suma es: ", suma
	Escribir "La resta es: ", resta
	Escribir "La multiplicación es: ", multiplicación
	Escribir "La división es: ", división
	Escribir "El módulo es: ", módulo
FinFuncion


//Ejercicio 3.
//Dados tres (3) números, Hacer una aplicación que calcule la resolvente.
Funcion Ejercicio_N3
	Definir a, b, c, discriminante, resolvente1, resolvente2, RaizCuadrada como Real
	Escribir "Ingrese el valor de a:"
	Leer a
	Escribir "Ingrese el valor de b:"
	Leer b
	Escribir "Ingrese el valor de c:"
	Leer c
	discriminante = b^2 - 4*a*c
	
	Si discriminante > 0 Entonces
		resolvente1 = (-b + RaizCuadrada*(discriminante)) / (2*a)
		resolvente2 = (-b - RaizCuadrada*(discriminante)) / (2*a)
		Escribir "Las resolventes son: ", resolvente1, " y ", resolvente2
	Sino Si discriminante = 0 Entonces
			resolvente1 = -b / (2*a)
			Escribir "La resolvente es: ", resolvente1
		Sino
			Escribir "La ecuación no tiene resolventes reales."
		FinSi
	FinSi
FinFuncion



//Ejercicio 4.
//Dados dos (2) lados de un triángulo en cm, calcular la hipotenusa del mismo.
Funcion Ejercicio_N4
	Definir lado1, lado2, hipotenusa como real;
	
    Escribir "Ingrese el valor del lado 1 (en cm):"
    Leer lado1
	
    Escribir "Ingrese el valor del lado 2 (en cm):"
    Leer lado2
	
    hipotenusa <- raiz(lado1^2 + lado2^2)
	
    Escribir "La hipotenusa del triángulo es:", hipotenusa, "cm"
FinFuncion


//Ejercicio 5.
//Dado un (1) número, imprimir 0 si es par y 1 si es impar.
Funcion Ejercicio_N5
	Definir num, resultado Como Entero
	
	Escribir "Ingrese un número:"
	Leer num
	resultado = num % 2
	
	Si resultado = 0 Entonces
		Escribir "El número es par"
		Escribir " Resultado: 0";
	Sino
		Escribir "El número es impar";
		Escribir "Resultado: 1";
	FinSi
FinFuncion


//Ejercicio 6
//Dado un (1) número binario de cuatro (4) dígitos imprimir su valor
Funcion Ejercicio_N6
	Definir numeroBinario Como Cadena
	Definir contadorBits1 Como Entero
	Definir bitParidad Como Entero
	
	Escribir "Ingrese un número binario de 4 dígitos:"
	Leer numeroBinario
	
	contadorBits1 = 0
	Para i = 1 Hasta Longitud(numeroBinario) Hacer
		Si Subcadena(numeroBinario, i, 1) = '1' Entonces
			contadorBits1 = contadorBits1 + 1
		Fin Si
	Fin Para
	
	Si contadorBits1 MOD 2 = 1 Entonces
		bitParidad = 1
	Sino
		bitParidad = 0
	Fin Si
	
	Escribir "El bit de paridad es: ", bitParidad
FinFuncion


//Ejercicio 7.
//Dado un (1) número de cuatro (4) dígitos imprimirlo por separado en unidades,decenas,
//centenas y unidades de mil.
Funcion Ejercicio_N7
	Definir num, unidades, decenas, centenas, unidadesMil Como Real
	
    Escribir "Ingrese un número de cuatro dígitos:"
    Leer num
    unidades = num % 10
    decenas = trunc(num%100)/ 10
    centenas = trunc(num%1000)/ 100
    unidadesMil = trunc(num/1000)
    Escribir "Unidades de Mil:", unidadesMil
    Escribir "Centenas:", centenas
    Escribir "Decenas:", decenas
    Escribir "Unidades:", unidades
FinFuncion


//Ejercicio 8
//Dado un carácter indicar si es un digito o una consonante o un carácter especial.
Funcion Ejercicio_N8
	Definir caracter como caracter
	
    Escribir "Ingrese un carácter:"
    Leer caracter
	
    Si caracter >= '0' Y caracter <= '9' Entonces
        Escribir "El carácter es un dígito."
    Sino
		Si (caracter >= 'A' Y caracter <= 'Z') O (caracter >= 'a' Y caracter <= 'z') Entonces
			Si caracter = 'A' O caracter = 'E' O caracter = 'I' O caracter = 'O' O caracter = 'U' O caracter = 'a' O caracter = 'e' O caracter = 'i' O caracter = 'o' O caracter = 'u' Entonces
				Escribir "El carácter es una vocal."
			Sino
				Escribir "El carácter es una consonante."
			FinSi
		Sino
			Escribir "El carácter es un carácter especial."
		FinSi
    Finsi
FinFuncion


//Ejercicio 9
//Dado dos caracteres indicar si el primer carácter es igual, mayor o menor que el segundo
Funcion  Ejercicio_N9
	Definir car1, car2 como Caracter
	Escribir "Ingrese el primer carácter:"
	Leer car1
	Escribir "Ingrese el segundo carácter:"
	Leer car2
	
	Si car1 = car2 Entonces
		Escribir "El primer carácter es igual al segundo"
	Sino Si car1 < car2 Entonces
			Escribir "El primer carácter es menor que el segundo"
		Sino
			Escribir "El primer carácter es mayor que el segundo"
		FinSi
	FinSi
FinFuncion


//Ejercicio 10
//Dada una frase cualquiera presentarla en mayúscula y minuscula
Funcion Ejercicio_N10
	Definir frase Como Caracter
	Definir x Como Entero
	Escribir "Ingresa un texto"
	leer frase
	Escribir "Converti a "
	Escribir "1 = Mayusculas"
	Escribir "2 = Minusculas"
	leer x
	
	si x > 0 y x < 3 Entonces
		si x == 1 Entonces
			Escribir "El texto en mayusculas es: ",Mayusculas(frase)
		sino
			Escribir "El texto en minusculas es: ",Minusculas(frase)
		FinSi
	SiNo
		Escribir "Ingresa una opcion correcta"
		FinSi
FinFuncion
// =======================================================Estructuras Condicionales =======================================================         


// Ejercicio 1: Año bisiesto
Funcion  Ejercicio1
    
    Escribir"Ingrese la fecha (aaaa, mes, día):"
    Leer aaaa, mes, dia
    
    Si ((aaaa % 400 = 0) O ((aaaa % 4 = 0) Y (aaaa % 100 <> 0))) Entonces
        Escribir "El año ", aaaa, " es un año bisiesto."
    Sino
        Escribir"El año ", aaaa, " no es un año bisiesto."
    Fin Si
FinFuncion


// Ejercicio 2: Número capicúa
Funcion  Ejercicio2
	
    Escribir"Ingrese un número de 5 dígitos:"
    Leer numero
    
    Si (numero / 10000 = numero % 10) Entonces
        Escribir"El número ", numero, " es capicúa."
    Sino
        Escribir"El número ", numero, " no es capicúa."
    Fin Si
FinFuncion



// Ejercicio 3: Conversión de horas y minutos a segundos
Funcion  Ejercicio3
	
    Escribir"Ingrese las horas:"
    Leer horas
    Escribir"Ingrese los minutos:"
    Leer minutos
    
    totalSegundos <- horas * 3600 + minutos * 60
    
    Escribir"El resultado en segundos es: ", totalSegundos
FinFuncion



// Ejercicio 4: Conversión de segundos a minutos, horas y días
Funcion Ejercicio4
    
    Escribir"Ingrese la cantidad de segundos:"
    Leer segundos
    
    minutos <- segundos / 60
    horas <- minutos / 60
    dias <- horas / 24
    
    Escribir"El equivalente en minutos es: ", minutos
    Escribir"El equivalente en horas es: ", horas
FinFuncion



// Ejercicio 5: Comparación de tres números
Funcion  Ejercicio5
	
    Escribir"Ingrese el primer número:"
    Leer A
    Escribir"Ingrese el segundo número:"
    Leer B
    Escribir"Ingrese el tercer número:"
    Leer C
    
    Si (A = B) Y (B = C) Entonces
        Escribir"Los tres números son iguales."
    Sino
        Si (A > B) Y (A > C) Entonces
            Escribir"El mayor número es: ", A
            Si (B > C) Entonces
                Escribir"El segundo mayor número es: ", B
            Sino
                Escribir"El segundo mayor número es: ", C
            Fin Si
        Sino
            Si (B > A) Y (B > C) Entonces
                Escribir"El mayor número es ", B
                Si (A > C) Entonces
                    Escribir"El segundo mayor número es: ", A
                Sino
                    Escribir"El segundo mayor número es: ", C
                Fin Si
            Sino
                Escribir"El mayor número es: ", C
                Si (A > B) Entonces
                    Escribir"El segundo mayor número es: ", A
                Sino
                    Escribir"El segundo mayor número es: ", B
                Fin Si
            Fin Si
        Fin Si
    Fin Si
FinFuncion



// Ejercicio 6: Cálculo del IMC
Funcion  Ejercicio6
	Definir LIBRAS_A_KILOGRAMOS Como Real
	Definir pesoLibras Como Real
	Definir estaturaCm Como Real
	Definir pesoKg Como Real
	Definir estaturaM Como Real
	Definir imc Como Real
	
	pesoLibras <- 0
	estaturaCm <- 0
	pesoKg <- 0
	estaturaM <- 0
	imc <- 0
	LIBRAS_A_KILOGRAMOS <- 0.453592
	
	Escribir "Ingrese el peso en libras: "
	Leer pesoLibras
	
	Escribir "Ingrese la estatura en centímetros: "
	Leer estaturaCm
	
	pesoKg <- pesoLibras * LIBRAS_A_KILOGRAMOS
	estaturaM <- estaturaCm / 100
	imc <- pesoKg / (estaturaM * estaturaM)
	
	Escribir "Peso en kilogramos: ", pesoKg
	Escribir "IMC: ", imc
	
	Si imc < 16 Entonces
		Escribir "Criterio de ingreso"
	Sino Si imc >= 16 Y imc <= 16.9 Entonces
			Escribir "Infra peso"
		Sino Si imc >= 17 Y imc <= 18.4 Entonces
				Escribir "Bajo peso"
			Sino Si imc >= 18.5 Y imc <= 24.9 Entonces
					Escribir "Peso normal"
				Sino Si imc >= 25 Y imc <= 29.9 Entonces
						Escribir "Sobrepeso"
					Sino Si imc >= 30 Y imc <= 34.9 Entonces
							Escribir "Obesidad pre-mórbida"
						Sino Si imc >= 40 Y imc <= 45 Entonces
								Escribir "Obesidad mórbida"
							Sino
								Escribir "Obesidad híper-mórbida"
							FinSi
						FinSi
					FinSi
				FinSi
			FinSi
		FinSi
	FinSi
FinFuncion



// Ejercicio 7: Cálculo de días transcurridos desde el 1 de Enero de 2014
Funcion  Ejercicio7
    
    Escribir"Ingrese la fecha (día, mes):"
    Leer dia, mes
    
    diasTranscurridos <- 0
    
    Segun mes Hacer
        1: diasTranscurridos <- dia
        2: diasTranscurridos <- 31 + dia
        3: diasTranscurridos <- 59 + dia
        4: diasTranscurridos <- 90 + dia
        5: diasTranscurridos <- 120 + dia
        6: diasTranscurridos <- 151 + dia
        7: diasTranscurridos <- 181 + dia
        8: diasTranscurridos <- 212 + dia
        9: diasTranscurridos <- 243 + dia
        10: diasTranscurridos <- 273 + dia
        11: diasTranscurridos <- 304 + dia
        12: diasTranscurridos <- 334 + dia
    Fin Segun
    
    Escribir"El número de días transcurridos desde el 1 de Enero de 2014 hasta la fecha dada es: ", diasTranscurridos
FinFuncion



// Ejercicio 8: Obtener el nombre del mes
Funcion  Ejercicio8
	
    Escribir"Ingrese un número entre 1 y 12:"
    Leer numeroMes
    
    Segun numeroMes Hacer
        1: Escribir"El mes correspondiente al número ", numeroMes, " es Enero."
        2: Escribir"El mes correspondiente al número ", numeroMes, " es Febrero."
        3: Escribir"El mes correspondiente al número ", numeroMes, " es Marzo."
        4: Escribir"El mes correspondiente al número ", numeroMes, " es Abril."
        5: Escribir"El mes correspondiente al número ", numeroMes, " es Mayo."
        6: Escribir"El mes correspondiente al número ", numeroMes, " es Junio."
        7: Escribir"El mes correspondiente al número ", numeroMes, " es Julio."
        8: Escribir"El mes correspondiente al número ", numeroMes, " es Agosto."
        9: Escribir"El mes correspondiente al número ", numeroMes, " es Septiembre."
        10: Escribir"El mes correspondiente al número ", numeroMes, " es Octubre."
        11: Escribir"El mes correspondiente al número ", numeroMes, " es Noviembre."
        12: Escribir"El mes correspondiente al número ", numeroMes, " es Diciembre."
        De Otro Modo: Escribir"Número de mes inválido. Por favor, ingrese un número entre 1 y 12."
    Fin Segun
FinFuncion



// Ejercicio 9: Aplicar descuento en una compra
Funcion  Ejercicio9
    
    Escribir"Ingrese el monto total de la compra:"
    Leer montoCompra
    Escribir"Ingrese el porcentaje de descuento (%):"
    Leer porcentajeDescuento
    
    descuento <- (montoCompra * porcentajeDescuento) / 100
    montoFinal <- montoCompra - descuento
    
    Escribir"El monto final de la compra, aplicando un descuento del ", porcentajeDescuento, "%, es: ", montoFinal
FinFuncion


// Ejercicio 10: Operaciones matemáticas
Funcion Ejercicio10
    
    Escribir"Ingrese el primer número:"
    Leer num1
    Escribir"Ingrese el segundo número:"
    Leer num2
    
    suma <- num1 + num2
    resta <- num1 - num2
    multiplicacion <- num1 * num2
    division <- num1 / num2
    
    Escribir"El resultado de la suma es: ", suma
    Escribir"El resultado de la resta es: ", resta
    Escribir"El resultado de la multiplicación es: ", multiplicacion
    Escribir"El resultado de la división es: ", division
FinFuncion



// =======================================================Estructuras Iterativas===========================================================    



//Ejercicio 1:Dado un número entero N, calcular e informar al usuario cuántos dígitos tiene dicho número
Funcion ContarDigitos
	Escribir "Ingrese un número: "
	Leer n
	
	digitos <- 0
	numeroAbsoluto <- abs(n)  
	
	cadena <- ConvertirATexto(numeroAbsoluto)
	digitos <- Longitud(cadena)
	
	Escribir "El número tiene ", digitos, " dígitos."
	
FinFuncion

//Ejercicio 2: Dado un número, determine si es capicúa.
//Nota: un número capicúa es aquel que se lee igual hacia adelante que hacia atrás
Funcion NumeroCapicua
	Escribir "Ingrese un número: "
	Leer numero
	
	numeroOriginal <- numero
	numeroInvertido <- 0
	
	Mientras numero > 0
		resto <- numero MOD 10
		numeroInvertido <- numeroInvertido * 10 + resto
		numero <- numero / 10
		numero <- trunc (numero)
	Fin Mientras
	
	Si numeroOriginal = numeroInvertido Entonces
		Escribir "El número es capicúa."
	Sino
		Escribir "El número no es capicúa."
	Fin Si
FinFuncion

//Ejercicio 3: Para un valor entero positivo que representa una cantidad en segundos, indicarsu
//equivalente en minutos, horas y días
Funcion ConversionTiempo
	Escribir "Ingrese la cantidad de segundos: "
	Leer segundos
	
	minutos <- segundos / 60
	horas <- minutos / 60
	dias <- horas / 24
	
	Escribir "Equivalente en minutos: ", minutos
	Escribir "Equivalente en horas: ", horas
	Escribir "Equivalente en días: ", dias
FinFuncion


//Ejercicio 4: Escribir un algoritmo que presente la suma de los divisores de un numero
Funcion DivisoresYSuma
	Escribir "Ingrese un número: "
	Leer numero
	
	Escribir "Los divisores de ", numero, " son:"
	suma <- 0
	
	Para divisor <- 1 Hasta numero
		Si numero MOD divisor = 0 Entonces
			Escribir divisor
			suma <- suma + divisor
		Fin Si
	Fin Para
	
	Escribir "La suma de los divisores de ", numero, " es:", suma
FinFuncion


// Ejercicio 5: Escribir un algoritmo que presente la cantidad de los divisores de un numero
Funcion CantidadDivisores
		Escribir "Ingrese un número: "
		Leer numero
		
		contadorDivisores <- 0
		
		Para divisor <- 1 Hasta numero Con Paso 1 Hacer
			Si numero MOD divisor = 0 Entonces
				contadorDivisores <- contadorDivisores + 1
			Fin Si
		Fin Para
		
		Escribir "La cantidad de divisores de", numero, "es:", contadorDivisores
FinFuncion

//Ejercicio 6: Escribir un algoritmo que indique si un número es perfecto
//Nota: un número es perfecto cuando la suma de los divisores del número incluido el 1 y
//excluido el propio número es igual al numero
//Ejemplo: numero=6: los divisores del 6 son: 1+2+3=6

Funcion  NumeroPerfecto
	Escribir "Ingrese un número: "
	Leer numero
	
	sumaDivisores <- 0
	
	Para divisor <- 1 Hasta numero / 2
		Si numero MOD divisor = 0 Entonces
			sumaDivisores <- sumaDivisores + divisor
		Fin Si
	Fin Para
	
	Si sumaDivisores = numero Entonces
		Escribir "El número ", numero, " es perfecto."
	Sino
		Escribir "El número ", numero, " no es perfecto."
	Fin Si
	
FinFuncion

//Ejercicio 7 = Dado un número N determinar si es un número primo.
//Nota: Un número primo es aquel que solo es divisible por 1(uno) y por el mismo.
Funcion NumeroPrimo
	Escribir "Ingrese un número: "
	Leer numero
	
	esPrimo <- Verdadero
	
	Si numero <= 1 Entonces
		esPrimo <- Falso
	Sino
		divisor <- 2
		Mientras divisor <= numero / 2 Y esPrimo
			Si numero MOD divisor = 0 Entonces
				esPrimo <- Falso
			Fin Si
			divisor <- divisor + 1
		Fin Mientras
	Fin Si
	
	Si esPrimo Entonces
		Escribir "El número ", numero, " es primo."
	Sino
		Escribir "El número ", numero, " no es primo."
	Fin Si

FinFuncion
//Ejercicio 8 = Construya un programa que dado un valor entero N, haga el cálculo de la función factorial
//utilizando estructuras iterativas.
Funcion FactorialIterativo
	Escribir "Ingrese un número: "
	Leer N
	
	factorial <- 1
	
	Si N >= 0 Entonces
		Para i <- 1 Hasta N Hacer
			factorial <- factorial * i
		Fin Para
		
		Escribir "El factorial de ", N, " es:", factorial
	Sino
		Escribir "El factorial no está definido para números negativos."
	Fin Si
	
FinFuncion


//ejercicio 9 = Dado un número entero N que representa una contraseña y asumiendo que una
//contraseña debe tener al menos 10 dígitos para ser segura, determine si la contraseña
//	ingresada por el usuario es correcta, de no serlo debe pedirla nuevamente hasta que tenga
//	los 10 (diez) dígitos solicitados y al ser correcta mostrar un mensaje de éxito al usuario, por salida estándar

Funcion VerificarContrasena
	valido <- Falso
	Mientras No valido
		Escribir "Ingrese una contraseña: "
		Leer contrasena
		
		Si Longitud(contrasena) >= 10 Entonces
			valido <- Verdadero
			Escribir "Contraseña válida. ¡Acceso concedido!"
		Sino
			Escribir "La contraseña debe tener al menos 10 dígitos. Inténtelo nuevamente."
		Fin Si
	Fin Mientras
	
FinFuncion


//Ejercicio 10= Dada una secuencia de números terminada en cero (0), elaborar un algoritmo que informe
//al usuario qué valor tiene el número mayor y qué valor tiene el número menor, sin contar
//el cero (0)

Funcion MayorMenorSinCero
	mayor <- 0
	menor <- 0
	primerNumero <- Verdadero
	continuar <- Verdadero
	
	Mientras continuar
		Escribir "Ingrese un número (0 para terminar): "
		Leer numero
		
		Si numero = 0 Entonces
			continuar <- Falso
		Sino
			Si primerNumero Entonces
				mayor <- numero
				menor <- numero
				primerNumero <- Falso
			Sino
				Si numero > mayor Entonces
					mayor <- numero
				Fin Si
				
				Si numero < menor Entonces
					menor <- numero
				Fin Si
			Fin Si
		Fin Si
	Fin Mientras
	
	Escribir "El número mayor es: ", mayor
	Escribir "El número menor es: ", menor
	
FinFuncion

//ejercicio 11= Se tiene una secuencia de enteros terminada en cero, que corresponde a la edad, peso y
//estatura de una muestra de hombres y mujeres mayores de 18 años.
Funcion EstudioMuestra
	totalEdad <- 0
	totalPeso <- 0
	totalEstatura <- 0
	contadorPersonas <- 0
	contadorPersonas1825 <- 0
	contadorPersonasMayor36 <- 0
	totalPeso1825 <- 0
	contadorPeso1825 <- 0
	continuar <- Verdadero
	
	Mientras continuar
		Escribir "Ingrese la edad (0 para terminar): "
		Leer edad
		
		Si edad = 0 Entonces
			continuar <- Falso
		Sino
			Escribir "Ingrese el peso: "
			Leer peso
			
			Escribir "Ingrese la estatura: "
			Leer estatura
			
			totalEdad <- totalEdad + edad
			totalPeso <- totalPeso + peso
			totalEstatura <- totalEstatura + estatura
			contadorPersonas <- contadorPersonas + 1
			
			Si edad >= 18 Y edad <= 25 Entonces
				contadorPersonas1825 <- contadorPersonas1825 + 1
			Fin Si
			
			Si edad > 36 Entonces
				contadorPersonasMayor36 <- contadorPersonasMayor36 + 1
			Fin Si
			
			Si edad >= 18 Y edad <= 35 Entonces
				totalPeso1825 <- totalPeso1825 + peso
				contadorPeso1825 <- contadorPeso1825 + 1
			Fin Si
		Fin Si
	Fin Mientras
	
	promedioEdad <- totalEdad / contadorPersonas
	promedioPeso <- totalPeso / contadorPersonas
	promedioEstatura <- totalEstatura / contadorPersonas
	
	Escribir "Edad promedio de todas las personas: ", promedioEdad
	Escribir "Peso promedio de todas las personas: ", promedioPeso
	Escribir "Estatura promedio de todas las personas: ", promedioEstatura
	Escribir "Cantidad de personas con edad entre 18 y 25 años: ", contadorPersonas1825
	Escribir "Cantidad de personas mayores a 36 años: ", contadorPersonasMayor36
	
	Si contadorPeso1825 > 0 Entonces
		promedioPeso1825 <- totalPeso1825 / contadorPeso1825
		Escribir "Promedio de peso de personas con edades entre 18 y 35 años: ", promedioPeso1825
	Fin Si
	

FinFuncion


//ejercicio 12= Construye un algoritmo que calcule e imprima la tabla de multiplicar, desde la tabla del 1
//hasta la del 10.
Funcion TablaDeMultiplicar
	para i <- 1 hasta 10 hacer
		Escribir "Tabla de multiplicar del ", i, ":"
		para j <- 1 hasta 10 hacer
			resultado <- i * j
			Escribir i, " x ", j, " = ", resultado
		fin para
		Escribir ""
	fin para
	
FinFuncion

//ejercicio 13= Escribir un algoritmo que multiplique dos números por medio de sumas sucesivas.
Funcion MultiplicacionSumasSucesivas
	Escribir "Ingrese el primer número: "
	Leer numero1
	
	Escribir "Ingrese el segundo número: "
	Leer numero2
	
	resultado <- 0
	contador <- 1
	
	mientras contador <= numero2 hacer
		resultado <- resultado + numero1
		contador <- contador + 1
	fin mientras
	
	Escribir "El resultado de la multiplicación es: ", resultado
FinFuncion


//ejercicio 14= Dados N número positivos (N>1) calcular el promedio de esta serie. Considere que la serie
//termina al leer un 0.

Funcion PromedioSerie
	total <- 0
	contador <- 0
	
	Escribir "Ingrese una serie de números positivos (0 para terminar):"
	Leer numero
	
	mientras numero <> 0 hacer
		total <- total + numero
		contador <- contador + 1
		Leer numero
	fin mientras
	
	si contador > 0 entonces
		promedio <- total / contador
		Escribir "El promedio de la serie es:", promedio
	sino
		Escribir "No se ingresaron números válidos."
	fin si
FinFuncion


 // ejercicio15= Escribir un algoritmo que divida dos números por medio de restas sucesivas.
Funcion DivisionRestasSucesivas
	Escribir "Ingrese el dividendo: "
	Leer dividendo
	
	Escribir "Ingrese el divisor: "
	Leer divisor
	
	cociente <- 0
	
	mientras dividendo >= divisor hacer
		dividendo <- dividendo - divisor
		cociente <- cociente + 1
	fin mientras
	
	Escribir "El cociente de la división es: ", cociente
	Escribir "El residuo de la división es: ", dividendo

FinFuncion

Algoritmo Proyecto
	Definir menuPrincipal,menuCondicionales,menuInteractivas,titulo,titulo2,opc,opcn,apcc,apca Como Caracter
	Definir pos,lim Como Entero
	Dimension menuPrincipal[4],menuSelectivas[11],menuCondicionales[11],menuInteractivas[16]
	// Arreglo menu principal
	menuPrincipal[1] = "  1)Ejercicios Estructuras Selectivas"
	menuPrincipal[2] = "  2)Ejercicios Estructuras Condicionales"
	menuPrincipal[3] = "  3)Ejercicios Estructuras Interactivas"
	menuPrincipal[4]= "  4)Salir"
	
	//submenu de Selectivas 
	menuSelectivas[1]= "convierta a expresión matemática"
	menuSelectivas[2]= "Dados dos números calcule la suma, resta, multiplicación, división y módulo"
	menuSelectivas[3]= "Dados tres números, Hacer una aplicación que calcule la resolvente."
	menuSelectivas[4]= "Dados dos lados de un triángulo en cm, calcular la hipotenusa del mismo."
	menuSelectivas[5]= "Dado un número, imprimir 0 si es par y 1 si es impar"
	menuSelectivas[6]= "Dado un número binario de cuatro dígitos imprimir su bit de paridad. "
	menuSelectivas[7]= "Dado un número de cuatro dígitos imprimirlo por separado en unidades,decenas,centenas y unidades de mil."
	menuSelectivas[8]= "Dado un carácter indicar si es un digito o una consonante o un carácter especial."
	menuSelectivas[9]= "Dado dos caracteres indicar si el primer carácter es igual, mayor o menor que el segundo"
	menuSelectivas[10]= "Dada una frase cualquiera presentarla en mayúscula y minuscula"
	menuSelectivas[11]= "Escriba el numero ^11^ para salir"
	
	// arreglo submenu Condicionales
	menuCondicionales[1]= "Ejercicio 1: Año bisiesto"
	menuCondicionales[2]= "Ejercicio 2: Número capicúa"
	menuCondicionales[3]= "Ejercicio 3: Conversión de horas y minutos a segundos"
	menuCondicionales[4]= "Ejercicio 4: Conversión de segundos a minutos, horas y días"
	menuCondicionales[5]= "Ejercicio 5: Comparación de tres números"
	menuCondicionales[6]= "Ejercicio 6: Cálculo del IMC "
	menuCondicionales[7]= "Ejercicio 7: Cálculo de días transcurridos desde el 1 de Enero de 2014"
	menuCondicionales[8]= "Ejercicio 8: Obtener el nombre del mes"
	menuCondicionales[9]= "Ejercicio 9: Aplicar descuento en una compra"
	menuCondicionales[10]= "Ejercicio 10: Operaciones matemáticas"
	menuCondicionales[11]= "Escriba el numero ^11^ para salir"
	
	//  submenu Interactivas 
	menuInteractivas[1] = "Ejercicio 1: Dado un número entero N, calcular e informar al usuario cuántos dígitos tiene dicho número"
	menuInteractivas[2] = "Ejercicio 2: Dado un número, determine si es capicúa."
	menuInteractivas[3] = "Ejercicio 3: Para un valor entero positivo que representa una cantidad en segundos, indicarsu equivalente en minutos, horas y días"
	menuInteractivas[4] = "Ejercicio 4: Escribir un algoritmo que presente la suma de los divisores de un numero"
	menuInteractivas[5] = "Ejercicio 5: Escribir un algoritmo que presente la cantidad de los divisores de un numero"
	menuInteractivas[6] = "Ejercicio 6: Escribir un algoritmo que indique si un número es perfecto"
	menuInteractivas[7] = "Ejercicio 7: Dado un número N determinar si es un número primo."
	menuInteractivas[8] = "Ejercicio 8: Construya un programa que dado un valor entero N, haga el cálculo de la función factorial "
	menuInteractivas[9] = "Ejercicio 9: Dado un número entero N que representa una contraseña"
	menuInteractivas[10] = "Ejercicio 10: Un algoritmo que informe al usuario qué valor tiene el número mayor y qué valor tiene el número menor, sin contar el cero (0) "
	menuInteractivas[11] = "Ejercicio 11: Se tiene una secuencia de enteros terminada en cero, que corresponde a la edad, peso y estatura "
	menuInteractivas[12] = "Ejercicio 12: Calcule e imprima la tabla de multiplicar, desde la tabla del 1 hasta la del 10."
	menuInteractivas[13] = "Ejercicio 13: Escribir un algoritmo que multiplique dos números por medio de sumas sucesivas"
	menuInteractivas[14] = "Ejercicio 14: (N>1) calcular el promedio de esta serie. Considere que la serie termina al leer un 0"
	menuInteractivas[15] = "Ejercicio 15: Escribir un algoritmo que divida dos números por medio de restas sucesivas. "
	menuInteractivas[16]= "Escriba el numero ^16^ para Salir"
	opc=""
    Mientras opc <> "4" Hacer
		opc=presentarMenu("Menu Principal",menuPrincipal,4)
		Segun opc Hacer
			"1":
				opcn=""
				Mientras opcn <> "11" Hacer         
					opcn=presentarMenu("Menu Selectivas",menuSelectivas,11)
					Segun opcn Hacer
						"1":
							Ejercicio_N1
							Esperar 3 Segundos
						"2":
							
							Ejercicio_N2
							Esperar 3 Segundos
						"3":
							
							Ejercicio_N3
							Esperar 3 Segundos
						"4":
							
							Ejercicio_N4
							Esperar 3 Segundos
						"5":
							
							Ejercicio_N5
							Esperar 3 Segundos
						"6":
							
							Ejercicio_N6
							Esperar 3 Segundos
						"7":
							
							Ejercicio_N7
							Esperar 3 Segundos
						"8":
							
							Ejercicio_N8
							Esperar 3 Segundos
						"9":
							
							Ejercicio_N9
							Esperar 3 Segundos
						"10":
							
							Ejercicio_N10
							Esperar 3 Segundos
						"11":
							Escribir "Regresando al Menu Principal"
							Esperar 3 Segundos
						De Otro Modo:
							Escribir "Opcion invalida..."
							Esperar 3 Segundos
					FinSegun
				FinMientras
			"2":
				opcn=""
				Mientras opcn <> "11" Hacer         
					opcn=presentarMenu("Menu Condicionales",menuCondicionales,11)
					Segun opcn Hacer
						"1":
							Ejercicio1
							Esperar 3 Segundos
						"2":
							
							Ejercicio2
							Esperar 3 Segundos
						"3":
							
							Ejercicio3
							Esperar 3 Segundos
						"4":
							
							Ejercicio4
							Esperar 3 Segundos
						"5":
							
							Ejercicio5
							Esperar 3 Segundos
						"6":
							
							Ejercicio6
							Esperar 3 Segundos
						"7":
							
							Ejercicio7
							Esperar 3 Segundos
						"8":
							
							Ejercicio8
							Esperar 3 Segundos
						"9":
							
							Ejercicio9
							Esperar 3 Segundos
						"10":
							
							Ejercicio10
							Esperar 3 Segundos
						"11":
							Escribir "Regresando al Menu Principal"
							Esperar 3 Segundos
						De Otro Modo:
							Escribir "Opcion invalida..."
							Esperar 3 Segundos
					FinSegun
				FinMientras
				
			"3":
				opcn=""
				Mientras opcn <> "16" Hacer         
					opcn=presentarMenu("Menu Interactivas",menuInteractivas,16)
					Segun opcn Hacer
						"1":
							
							ContarDigitos
							Esperar 3 Segundo
						"2":
							
							NumeroCapicua
							Esperar 3 Segundos
						"3":
							
							ConversionTiempo
							Esperar 3 Segundos
						"4":
							
							DivisoresYSuma
							Esperar 3 Segundos
						"5":
							
							CantidadDivisores
							Esperar 3 Segundos
						"6":
							
							NumeroPerfecto
							Esperar 3 Segundos
						"7":
							
							NumeroPrimo
							Esperar 3 Segundos
						"8":
							
							FactorialIterativo
							Esperar 3 Segundos
						"9":
							
							VerificarContrasena
							Esperar 3 Segundos
						"10":
							
							MayorMenorSinCero
							Esperar 3 Segundos
						"11":
							
							EstudioMuestra
							Esperar 3 Segundos
						"12":
							
							TablaDeMultiplicar
							Esperar 3 Segundos
							
						"13":
							
							MultiplicacionSumasSucesivas
							Esperar 3 Segundos
						"14":
							
							PromedioSerie
							Esperar 3 Segundos
							
						"15":
							
							DivisionRestasSucesivas
							Esperar 3 Segundos
						"16":
							Escribir "Regresando al Menu Principal"
							Esperar 3 Segundos
						De Otro Modo:
							Escribir "Opcion invalida..."
							Esperar 3 Segundos
					FinSegun
				FinMientras
			"4":
				Escribir "Gracias por usar el Sistema"
				Esperar 3 Segundos
		Fin Segun
	FinMientras
FinAlgoritmo
